/*
 * Copyright 2019 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
  id = 'cd.go.authentication.ldap'
  pluginVersion = '2.2.0'
  goCdVersion = '20.9.0'
  name = 'LDAP Authentication Plugin for GoCD'
  description = 'LDAP Authentication Plugin for GoCD'
  vendorName = 'ThoughtWorks, Inc.'
  vendorUrl = 'https://github.com/gocd/gocd-ldap-authentication-plugin'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'gocd-ldap-authentication-plugin'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project

  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project.tasks.jar]

  licenseReport {
    excludeGroups = ["cd.go.plugin.base"]
  }
}

group = 'cd.go'
version = gocdPlugin.fullVersion(project)

repositories {
  mavenCentral()
  mavenLocal()
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

sourceSets {
  integrationTest {
    java.srcDirs = ['src/integration']
    resources.srcDirs += ['src/testdata']
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
  compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '22.1.0'
  implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
  implementation group: 'cd.go.plugin.base', name: 'gocd-plugin-base', version: '0.0.4'
  implementation group: 'org.apache.directory.api', name: 'api-ldap-client-api', version: '1.0.3'
  implementation group: 'org.apache.directory.api', name: 'api-ldap-codec-standalone', version: '1.0.0'

  testImplementation group: 'cd.go.plugin', name: 'go-plugin-api', version: '21.4.0'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.6.1'
  testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.2'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.8.2'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'

  //This is used in integration test
  integrationTestImplementation group: 'junit', name: 'junit', version: '4.13.2'
  integrationTestImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'
  integrationTestImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'
  integrationTestImplementation group: 'org.apache.directory.server', name: 'apacheds-core', version: '2.0.0.AM26'
  integrationTestImplementation group: 'org.apache.directory.server', name: 'apacheds-core-annotations', version: '2.0.0.AM26'
  integrationTestImplementation group: 'org.apache.directory.server', name: 'apacheds-core-integ', version: '2.0.0-M24'
  integrationTestImplementation group: 'org.apache.directory.server', name: 'apacheds-core-jndi', version: '2.0.0.AM26'
  integrationTestImplementation group: 'org.apache.directory.server', name: 'apacheds-core-constants', version: '2.0.0.AM26'
  integrationTestRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.8.2'
}

test {
  useJUnitPlatform()
}

task integrationTest(type: Test, description: 'Runs the ldap integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  mustRunAfter tasks.test
  useJUnitPlatform()
}

check.dependsOn integrationTest

jar {
  from(configurations.runtimeClasspath) {
    into "lib/"
  }
}
